clear
reset

set output 'avg-time.svg'

set terminal svg size 1200,300 enhanced background rgb 'white' fname 'Helvetica,20' lw 2

set encoding utf8

# Line colors - Disk
lenet   = "#808b96" 
resnet  = "#000000"
alexnet = "#808080"

set style line 1 linecolor rgb resnet
set style line 2 linecolor rgb alexnet
set style line 3 linecolor rgb lenet

#chart style
set style data histogram
set style fill solid
set style histogram cluster gap 1
set boxwidth 0.9
set xtics format ""
set grid ytics
set ytics 200
set yrange [0:*]
unset xtics

set grid ytics mytics  # draw lines for each ytics and mytics
set mytics 2           # set the spacing for the mytics
set grid 

set label 1 "LeNet" font "Helvetica, 22" at screen 0.272, 0.12
set label 2 "AlexNet" font "Helvetica, 22" at screen 0.53, 0.12
set label 3 "ResNet" font "Helvetica, 22" at screen 0.791, 0.12

### Start multiplot (1x3 layout)
set multiplot layout 1,3 rowsfirst margin 0.2,0.92,0.2,0.75 spacing 0.055,0.1

# --- GRAPH 1-0
set ylabel "Training time (s)" offset 1.5,0,0
set key font 'Helvetica,12' \
    maxrows 1 \
    Left reverse \
    width -5 \
    at screen 0.935, 0.85

plot '.\\vanilla-lustre-100g\\aggregate-lenet\\train-time.txt' using 2 every ::1::1 ti "vanilla-lustre" with histogram ls 1, \
    '.\\caching\\aggregate-lenet\\train-time.txt' using 2 every ::1::1 ti "vanilla-caching" with histogram ls 2, \
    '.\\vanilla-local\\aggregate-lenet\\train-time.txt' using 2 every ::1::1 ti "vanilla-local" with histogram ls 3

unset format x
unset key 
unset label 1
unset label 2
unset label 3

# --- GRAPH 2-0

unset ylabel
plot '.\\vanilla-lustre-100g\\aggregate-alexnet\\train-time.txt' using 2 every ::1::1 with histogram ls 1, \
    '.\\caching\\aggregate-alexnet\\train-time.txt' using 2 every ::1::1 with histogram ls 2, \
    '.\\vanilla-local\\aggregate-alexnet\\train-time.txt' using 2 every ::1::1 with histogram ls 3

unset key 

# --- GRAPH 3-0

set ytics 500

unset ylabel
plot '.\\vanilla-lustre-100g\\aggregate-resnet\\train-time.txt' every ::1::1 using 2 with histogram ls 1, \
    '.\\caching\\aggregate-resnet\\train-time.txt' every ::1::1 using 2 with histogram ls 2, \
    '.\\vanilla-local\\aggregate-resnet\\train-time.txt' every ::1::1 using 2 with histogram ls 3


unset multiplot
quit